module Effect.Error
: Module

import Data.Either
import Data.Function

interface Error : (E : Type) -> Interface
{ throw : { A : Type } -> E -> [Error E] A
, catch : { A : Type } -> [Error E] A -> (E -> [Error E] A) -> [Error E] A }

handle : { E, A : Type } -> (h : E -> [Error E] A) -> (a : [Error E] A) -> [Error E] A
{ catch a h }

try : { E, A : Type } -> (a : [Error E] A) -> [Error E] Either E A
{ handle inl (inr a) }

except : { E, A : Type } -> Either E A -> [Error E] A
{ e -> either throw id e }


# Handlers

runError : { E, A : Type } -> [Error E] A -> Either E A
{ [e] -> error inl inr e }

error
:  { E, A, R : Type }
-> (failure : E -> R)
-> (success : A -> R)
-> [Error E] A
-> R
{ [ throw e   ; _ ] -> failure e
, [ catch a h ; k ] -> error (error failure success << k << h) (error failure success << k) a
, a                 -> success a }
